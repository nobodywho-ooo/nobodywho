# Unity Development Tasks
set shell := ["bash", "-c"]
set dotenv-load := true

# Cache configuration
CACHE_DIR := ".temp-project-cache"
PROJECT_CACHE := CACHE_DIR + "/project-template"
MODEL_CACHE := CACHE_DIR + "/models"
PACKAGE_CACHE := CACHE_DIR + "/packages"
TEMP_PROJECT := "temp-project"

MODEL_FILENAME := "qwen2.5-1.5b-instruct-q4_0.gguf"
MODEL_URL := "https://huggingface.co/Qwen/Qwen2.5-1.5B-Instruct-GGUF/resolve/main/qwen2.5-1.5b-instruct-q4_0.gguf"
EMBEDDING_MODEL_FILENAME := "bge-small-en-v1.5-q8_0.gguf"
EMBEDDING_MODEL_URL := "https://huggingface.co/CompendiumLabs/bge-small-en-v1.5-gguf/resolve/main/bge-small-en-v1.5-q8_0.gguf"


default:
    just test

_ensure-cache-dirs:
    mkdir -p "{{PROJECT_CACHE}}" "{{MODEL_CACHE}}" "{{PACKAGE_CACHE}}"

_ensure-license:
    #!/usr/bin/env bash
    LICENSE_PATH=~/.local/share/unity3d/Unity/Unity_lic.ulf
    
    if [ ! -f "$LICENSE_PATH" ]; then
        echo "üîë No Unity license found at $LICENSE_PATH"
        echo ""
        echo "Please follow these steps:"
        echo "1. Run: unityhub"
        echo "2. Log in with your Unity account"
        echo "3. Go to Preferences > Licenses"
        echo "4. Click Add > Get a free personal license"
        echo ""
        echo "The license file will be created automatically at:"
        echo "$LICENSE_PATH"
        echo ""
        echo "Then run 'just' again to launch Unity"
        exit 1
    fi

_hash-source-files:
    #!/usr/bin/env bash
    find src -type f -exec sha256sum {} \; | sort | sha256sum | cut -d' ' -f1

_ensure-temp-project: _ensure-license
    #!/usr/bin/env bash
    # Ensure jq is installed
    if ! command -v jq &> /dev/null; then
        echo "‚ùå jq command could not be found. Please install jq."
        exit 1
    fi

    # Create project and configure manifest only if project doesn't exist
    if [ ! -d "{{TEMP_PROJECT}}" ]; then
        echo "üî® Creating new project '{{TEMP_PROJECT}}'..."
        unity-editor \
            -batchmode \
            -createProject "{{TEMP_PROJECT}}" \
            -quit
        
        
        MANIFEST_JSON_PATH="{{TEMP_PROJECT}}/Packages/manifest.json"
        
        if [ ! -f "$MANIFEST_JSON_PATH" ]; then
            echo "‚ùå $MANIFEST_JSON_PATH not found after project creation!"
            rm -rf "{{TEMP_PROJECT}}"
            exit 1
        fi

        echo "üìù Configuring $MANIFEST_JSON_PATH for package mode..."
        jq \
          '
            .dependencies["com.nobodywho.unity"] = "file:../../src" |
            .dependencies["com.unity.test-framework"] = "1.4.5" |
            .dependencies["com.unity.testtools.codecoverage"] = "1.1.1" |
            .testables = ["com.nobodywho.unity"]
          ' \
          "$MANIFEST_JSON_PATH" > "$MANIFEST_JSON_PATH.tmp"
        
        if [ $? -ne 0 ]; then
            echo "‚ùå jq command failed to update $MANIFEST_JSON_PATH!"
            rm -f "$MANIFEST_JSON_PATH.tmp" # Clean up temp file on error
            exit 1
        fi
        mv "$MANIFEST_JSON_PATH.tmp" "$MANIFEST_JSON_PATH"
        echo "‚úÖ manifest.json configured for package 'com.nobodywho.unity'."
    else
        echo "‚òëÔ∏è Temporary project '{{TEMP_PROJECT}}' already exists. manifest.json assumed to be configured."
    fi
    
    # Ensure model files exist in StreamingAssets (runs every time)
    STREAMING_ASSETS="{{TEMP_PROJECT}}/Assets/StreamingAssets"
    mkdir -p "$STREAMING_ASSETS"
    if [ ! -f "$STREAMING_ASSETS/{{MODEL_FILENAME}}" ]; then
        echo "üì• Downloading model..."
        curl -L --fail --progress-bar "{{MODEL_URL}}" -o "$STREAMING_ASSETS/{{MODEL_FILENAME}}"
    fi

    if [ ! -f "$STREAMING_ASSETS/{{EMBEDDING_MODEL_FILENAME}}" ]; then
        echo "üì• Downloading embedding model..."
        curl -L --fail --progress-bar "{{EMBEDDING_MODEL_URL}}" -o "$STREAMING_ASSETS/{{EMBEDDING_MODEL_FILENAME}}"
    fi
    
    echo "‚úÖ Temporary project ready at: {{TEMP_PROJECT}}"

# Clean all caches and temporary files
clean:
    rm -rf "{{TEMP_PROJECT}}"
    rm -rf "{{CACHE_DIR}}"

build-plugin: _ensure-temp-project
    #!/usr/bin/env bash
    
    cargo build
    if [ $? -ne 0 ]; then
        echo "‚ùå Cargo build failed!"
        exit 1
    fi

    cp "../target/debug/libnobodywho_unity.so" "src/Runtime/Plugins/x86_64-unknown-linux-gnu/libnobodywho_unity.so"
    
    if [ $? -ne 0 ]; then
        echo "‚ùå Failed to copy plugin to temp project!"
        exit 1
    fi
    # TODO: ensure that the manifest.json is correctly added.
    echo "‚úÖ Build complete!"

run-unity: _ensure-temp-project
    #!/usr/bin/env bash
    echo "üöÄ Running Unity..."
    unity-editor -projectPath "{{TEMP_PROJECT}}"

test VERBOSE="false": _ensure-temp-project build-plugin
    #!/usr/bin/env bash

    rm -rf "$(pwd)/unity-test.log"
    echo "üöÄ Running Unity tests..."

    # Start Unity in background and stream its log
    unity-editor \
        -projectPath "{{TEMP_PROJECT}}" \
        -batchmode \
        -runTests \
        -testResults "$(pwd)/test-results.xml" \
        -testPlatform PlayMode \
        -logFile "$(pwd)/unity-test.log" &

    UNITY_PID=$!

    # wait for the editor to start, otherwise we cant find the log file
    sleep 0.5
    tail -f unity-test.log &
    TAIL_PID=$!

    wait $UNITY_PID
    UNITY_EXIT_CODE=$?
    
    kill $TAIL_PID
    echo "Unity exited with code: $UNITY_EXIT_CODE"
    
    TEST_RESULT=0
    just _parse-result || TEST_RESULT=$?
    
    rm -rf "$(pwd)/test-results.xml"
    exit $TEST_RESULT

_parse-result:
    #!/usr/bin/env bash
    # Get test results if they exist
    if [ -f "$(pwd)/test-results.xml" ]; then
        RESULTS=$(just _parse-test-results)
        IFS=':' read -r TOTAL PASSED FAILED SKIPPED DURATION <<< "$RESULTS"
        
        echo ""
        echo "üìä Test Results Summary:"
        echo "===================="
        
        # Display summary
        echo "‚ú® Total Tests: $TOTAL"
        echo "‚úÖ Passed: $PASSED"
        echo "‚ùå Failed: $FAILED"
        echo "‚è±Ô∏è  Duration: $DURATION seconds"
        echo ""
        
        # If there are failures, show them
        if [ "$FAILED" -gt 0 ]; then
            echo "Failed Tests:"
            echo "============"
            echo ""
            
            # Get all failed test names
            FAILED_TESTS=$(xmllint --xpath "//test-case[@result='Failed']/@name" test-results.xml 2>/dev/null | tr ' ' '\n' | sed 's/name="\(.*\)"/\1/')
            
            # For each failed test, get its message
            echo "$FAILED_TESTS" | while IFS= read -r test_name; do
                if [ ! -z "$test_name" ]; then
                    # Get the failure message for this test
                    message=$(xmllint --xpath "string(//test-case[@name='$test_name']//failure/message)" test-results.xml 2>/dev/null | sed 's/\[\[CDATA\[\(.*\)\]\]\>/\1/')
                    
                    echo "‚ùå $test_name"
                    echo "-------------------"
                    echo "$message"
                    echo ""
                fi
            done
        fi
        
        # Return status code based on test results
        if [ "$FAILED" -gt 0 ]; then
            exit 1
        else
            exit 0
        fi
    else
        echo "‚ùå No test results XML file was generated"
        exit 1
    fi

_parse-test-results:
    #!/usr/bin/env bash
    if [ ! -f "test-results.xml" ]; then
        echo "‚ùå No test results found!"
        exit 1
    fi
    
    # Parse the XML file using xmllint with default values if not found
    TOTAL=$(xmllint --xpath "string(/test-run/@total)" test-results.xml 2>/dev/null || echo "0")
    PASSED=$(xmllint --xpath "string(/test-run/@passed)" test-results.xml 2>/dev/null || echo "0")
    FAILED=$(xmllint --xpath "string(/test-run/@failed)" test-results.xml 2>/dev/null || echo "0")
    SKIPPED=$(xmllint --xpath "string(/test-run/@skipped)" test-results.xml 2>/dev/null || echo "0")
    DURATION=$(xmllint --xpath "string(/test-run/@duration)" test-results.xml 2>/dev/null || echo "0")
    
    # Convert empty strings to 0
    TOTAL=${TOTAL:-0}
    PASSED=${PASSED:-0}
    FAILED=${FAILED:-0}
    SKIPPED=${SKIPPED:-0}
    DURATION=${DURATION:-0}
    
    # Output in a format that can be captured
    echo "$TOTAL:$PASSED:$FAILED:$SKIPPED:$DURATION"

